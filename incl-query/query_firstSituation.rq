prefix ex: <http://example.org/virtualhome2kg/instance/> 
prefix ho: <http://www.owl-ontologies.com/VirtualHome.owl#> 
prefix ns1: <http://purl.org/dc/terms/> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix time: <http://www.w3.org/2006/time#> 
prefix vh2kg: <http://example.org/virtualhome2kg/ontology/> 
prefix vh2kg-an: <http://example.org/virtualhome2kg/ontology/action/> 
prefix x3do: <https://www.web3d.org/specifications/X3dOntology4.0#> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
select distinct ?firstEvent ?firstSituation ?state ?objectType ?objectLabel ?objectId ?BCX ?BCY ?BCZ ?BSX ?BSY ?BSZ where { 
	?activity vh2kg:hasEvent ?firstEvent.
    ?firstEvent a vh2kg:StartEvent;
                vh2kg:situationBeforeEvent ?firstSituation.
    ?state vh2kg:partOf ?firstSituation;
           vh2kg:bbox ?shape;
           vh2kg:isStateOf ?object.
    ?shape x3do:bboxCenter ?bboxCenter;
           x3do:bboxSize ?bboxSize;
           a x3do:Shape.
    ?bboxCenter rdf:first ?BCX.
    ?bboxCenter rdf:rest ?BCYb .
    ?BCYb rdf:first ?BCY ;
          rdf:rest ?BCZb .
    ?BCZb rdf:first ?BCZ .
    ?bboxSize rdf:first ?BSX.
    ?bboxSize rdf:rest ?BSYb .
    ?BSYb rdf:first ?BSY  ;
          rdf:rest ?BSZb .
    ?BSZb rdf:first ?BSZ .
    ?object a ?objectType;
            rdfs:label ?objectLabel;
            ns1:identifier ?objectId.

    filter(?activity = PLACE_HOLDER)

}
